script_dir := `pwd`
neard := script_dir / "./neard"
near_localnet_home := script_dir / ".near"
near_accounts_path := script_dir / "user-data"
near_config_file := near_localnet_home / "config.json"
near_genesis_file := near_localnet_home / "genesis.json"
near_ft_contract_path := script_dir / "./fungible_token.wasm"
rpc_url := "http://127.0.0.1:4040"


init-localnet:
    rm -rf {{near_localnet_home}} && rm -rf {{near_accounts_path}}
    {{neard}} --home {{near_localnet_home}} init --chain-id localnet
    jq '.chain_id="benchmarknet"' {{near_genesis_file}} > tmp_genesis.json && mv tmp_genesis.json {{near_genesis_file}}
    jq '.rpc.addr="0.0.0.0:4040"' {{near_config_file}} > tmp_config.json && mv tmp_config.json {{near_config_file}}

run-localnet loglevel="info":
    RUST_LOG={{loglevel}} \
    {{neard}} --home {{near_localnet_home}} run

create-accounts num_accounts="500":
    #!/usr/bin/env bash
    {{neard}} --home {{near_localnet_home}} run & NEARD_PID=$!
    sleep 5
    RUST_LOG=info \
    ../synth-bm/target/release/near-synth-bm create-sub-accounts \
        --rpc-url {{rpc_url}} \
        --signer-key-path {{near_localnet_home}}/validator_key.json \
        --nonce 1 \
        --sub-account-prefix 'a' \
        --num-sub-accounts {{num_accounts}} \
        --deposit 200002488684649026500000000 \
        --channel-buffer-size 1200 \
        --requests-per-second 200  \
        --user-data-dir user-data
    kill ${NEARD_PID}
    sleep 3

# Enables the transaction generator in the localnet config.
# Updates the accounts_path field to point to the created accounts and merges the `tx-generator-settings.json` into config file
# If `tx-generator-settings.json` does not exist, it is copied from `tx-generator-settings.json.in` .
enable-tx:
    cp -n tx-generator-settings.json.in tx-generator-settings.json
    jq '.tx_generator.accounts_path = "{{near_accounts_path}}"' tx-generator-settings.json > {{near_localnet_home}}/tx-generator-settings.tmp.json
    jq -s '.[0] * .[1]' {{near_config_file}} {{near_localnet_home}}/tx-generator-settings.tmp.json > tmp_config.json && mv tmp_config.json {{near_config_file}}
    rm {{near_localnet_home}}/tx-generator-settings.tmp.json

enable-ft:
    cp -n tx-generator-settings.json.in tx-generator-settings.json
    jq '.tx_generator.accounts_path = "{{near_accounts_path}}" \
     |  .tx_generator.transaction_type.FungibleToken.wasm_path = "{{near_ft_contract_path}}" \
       ' tx-generator-settings.json > {{near_localnet_home}}/tx-generator-settings.tmp.json
    jq -s '.[0] * .[1]' {{near_config_file}} {{near_localnet_home}}/tx-generator-settings.tmp.json > tmp_config.json && mv tmp_config.json {{near_config_file}}
    rm {{near_localnet_home}}/tx-generator-settings.tmp.json

unlimit:
    jq '.gas_limit=20000000000000000' {{near_genesis_file}} > tmp_genesis.json && mv tmp_genesis.json {{near_genesis_file}}
    # Some versions of jq emit 2e+16 instead of plain integer, which breaks genesis parsing. To prevent this replace 2e+16 with the integer.
    sed -i 's/"gas_limit": 2e+16/"gas_limit": 20000000000000000/g' {{near_genesis_file}}
    jq '.view_client_threads=8 \
     | .store.load_mem_tries_for_tracked_shards=true \
     | .produce_chunk_add_transactions_time_limit={"secs": 0, "nanos": 500000000} ' {{near_config_file}} > tmp_config.json \
    && mv tmp_config.json {{near_config_file}}

do-it:
    #!/usr/bin/env bash
    just init-localnet
    just unlimit
    just create-accounts
    just enable-tx
    just run-localnet

do-ft:
    #!/usr/bin/env bash
    just init-localnet
    just unlimit
    just create-accounts
    just enable-ft
    just run-localnet
