script_dir := `pwd`
neard := script_dir / "./neard"
near_localnet_home := script_dir / ".near"
near_accounts_path := script_dir / "user-data"
near_config_file := near_localnet_home / "config.json"
near_genesis_file := near_localnet_home / "genesis.json"
rpc_url := "http://127.0.0.1:4040"


init-localnet:
    rm -rf {{near_localnet_home}} && rm -rf {{near_accounts_path}}
    {{neard}} --home {{near_localnet_home}} init --chain-id localnet
    jq '.chain_id="benchmarknet"' {{near_genesis_file}} > tmp_genesis.json && mv tmp_genesis.json {{near_genesis_file}}
    jq '.rpc.addr="0.0.0.0:4040"' {{near_config_file}} > tmp_config.json && mv tmp_config.json {{near_config_file}}


run-localnet loglevel="info":
    RUST_LOG={{loglevel}} \
    {{neard}} --home {{near_localnet_home}} run

create-accounts num_accounts="50000":
    #!/usr/bin/env bash
    {{neard}} --home {{near_localnet_home}} run &
    NEARD_PID=$!
    sleep 5
    RUST_LOG=info \
    ../synth-bm/target/release/near-synth-bm create-sub-accounts \
        --rpc-url {{rpc_url}} \
        --signer-key-path {{near_localnet_home}}/validator_key.json \
        --nonce 1 \
        --sub-account-prefix 'a' \
        --num-sub-accounts {{num_accounts}} \
        --deposit 953060601875000000010000 \
        --channel-buffer-size 1200 \
        --requests-per-second 200  \
        --user-data-dir user-data
    kill ${NEARD_PID}
    sleep 3

enable-tx:
    cp -n tx-generator-settings.json.in tx-generator-settings.json
    jq '.tx_generator={ \
        "accounts_path": "{{near_accounts_path}}"}' {{near_config_file}} > tmp_config.json && mv tmp_config.json {{near_config_file}}
    jq --slurpfile patch tx-generator-settings.json ' \
      . as $orig \
      | $patch[0].tx_generator.schedule as $sched    \
      | .["tx_generator"] += { "schedule": $sched }  \
      | $patch[0].tx_generator.controller as $ctrl   \
      | .["tx_generator"] += { "controller": $ctrl } \
    ' {{near_config_file}} > tmp_config.json && mv tmp_config.json {{near_config_file}}

unlimit:
    jq '.gas_limit=20000000000000000' {{near_genesis_file}} > tmp_genesis.json && mv tmp_genesis.json {{near_genesis_file}}
    # Some versions of jq emit 2e+16 instead of plain integer, which breaks genesis parsing. To prevent this replace 2e+16 with the integer.
    sed -i 's/"gas_limit": 2e+16/"gas_limit": 20000000000000000/g' {{near_genesis_file}}
    jq '.view_client_threads=8 \
     | .store.load_mem_tries_for_tracked_shards=true \
     | .produce_chunk_add_transactions_time_limit={"secs": 0, "nanos": 500000000} ' {{near_config_file}} > tmp_config.json \
    && mv tmp_config.json {{near_config_file}}

do-it:
    #!/usr/bin/env bash
    just init-localnet
    just unlimit
    just create-accounts
    just enable-tx
    just run-localnet
